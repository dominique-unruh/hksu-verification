include "game1FO_goodbad_o2h_right.qrhl".
include "axioms.qrhl".

# GRAPH: ONESIDED("game1FO_goodbad_o2h_right_count","game1FO_goodbad_o2h_right",uses="Adv_INDCCA_encFO_count_decapsQuery1_G'")
lemma game1FO_goodbad_o2h_right_count: Pr[count<=qG+2*qH+qD+1 : game1FO_goodbad_o2h_right(rho)] = 1.
 byqrhl.

 # This gets rid of (useless) occurrences of quantum variables which makes the "local remove left" tactics
 # below go through.
 conseq pre: Cla[True].
  simp!.

 inline game1FO_goodbad_o2h_right.
 inline Adv_O2H_Game1FO.
 inline ClassicalQueryG.
 inline Count.
 inline queryG'.

 local up left.
 local remove left.
  simp!.

 seq <-> 20 0: Cla[count1=1].
 conseq qrhl: Adv_INDCCA_encFO_count_decapsQuery1_G'.
  simp!.
  simp!.

 wp 7 0.
 conseq post: Cla[count1=1].
  simp!.
 wp left.
 conseq post: Cla[count1=0].
  simp!.
 wp 11 0.
 conseq post: Cla[count1=0].
  simp!.
 wp left.
 skip.
 simp!.
qed.
